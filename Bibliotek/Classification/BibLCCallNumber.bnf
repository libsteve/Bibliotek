;
; BibLCCallNumber.bnf
; Bibliotek
;
; A grammar for Library of Congress classification and call numbers.
;

; MARK: ------------------------------------------------------------------------
; MARK: Whitespace and Words

      <space> ::= ' ' | '\n' | '\t' | <space> <space>
    <nospace> ::= '.' | '-' | <letter> | <digit> | ...

       <word> ::= <nospace>
                | <nospace> <nospace>
                | <nospace> <nospace> <nospace>
                | <nospace> <nospace> <nospace> <nospace>
   <longword> ::= <word>
                | <word> <word>
                | <word> <word> <word>
                | <word> <word> <word> <word>

; MARK: ------------------------------------------------------------------------
; MARK: Digits

      <digit> ::= '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
    <digit02> ::= <digit>
                | <digit> <digit>
    <digit03> ::= <digit>
                | <digit> <digit>
                | <digit> <digit> <digit>
    <digit04> ::= <digit>
                | <digit> <digit>
                | <digit> <digit> <digit>
                | <digit> <digit> <digit> <digit>
    <digit16> ::= <digit04>
                | <digit04> <digit04>
                | <digit04> <digit04> <digit04>
                | <digit04> <digit04> <digit04> <digit04>

; MARK: ------------------------------------------------------------------------
; MARK: Lowercase letters

      <lower> ::= 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j'
                | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't'
                | 'u' | 'v' | 'w' | 'x' | 'y' | 'z'
    <lower04> ::= <lower>
                | <lower> <lower>
                | <lower> <lower> <lower>
                | <lower> <lower> <lower> <lower>
    <lower16> ::= <lower04>
                | <lower04> <lower04>
                | <lower04> <lower04> <lower04>
                | <lower04> <lower04> <lower04> <lower04>

; MARK: ------------------------------------------------------------------------
; MARK: Uppercase letters

    <upper> ::= 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J'
              | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T'
              | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z'

; MARK: ------------------------------------------------------------------------
; MARK: Alphabetic characters

     <letter> ::= <upper> | <lower>
   <letter04> ::= <letter>
                | <letter> <letter>
                | <letter> <letter> <letter>
                | <letter> <letter> <letter> <letter>
   <letter15> ::= <letter04>
                | <letter04> <letter04>
                | <letter04> <letter04> <letter04>
                | <letter04> <letter04> <letter04> <letter>
                | <letter04> <letter04> <letter04> <letter> <letter>
                | <letter04> <letter04> <letter04> <letter> <letter> <letter>

; MARK: ------------------------------------------------------------------------
; MARK: Date Primitives

  <year-full> ::= <digit> <digit> <digit> <digit>
  <year-abbr> ::= <digit> <digit>
 <month-abbr> ::= "Jan"  | "Feb" | "Mar" | "Apr"
                | "May"  | "Jun" | "Jul" | "Aug"
                | "Sept" | "Oct" | "Nov" | "Dec"
  <day-digit> ::= '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
 <day-number> ::= <day-digit>
                | '0' <day-digit>
                | '1' <digit>
                | '2' <digit>
                | '3' '0'
                | '3' '1'
  <date-mark> ::= <letter04>

; MARK: ------------------------------------------------------------------------
; MARK: Date Components

      <day-component> ::= <day-number>
    <month-component> ::= <month-abbr>
<month-day-component> ::= <month-component> <month-day-divider> <day-component>
  <month-day-divider> ::= '.'
                        | <space>
                        | '.' <space>

     <year-component> ::= <year-full>
                        | <year-abbr>
 <year-month-divider> ::= <space>
                        | ',' <space>

  <date-span-divider> ::= '-' | '/'
<date-span-component> ::= <year-component> <date-span-divider> <year-component>

       <date> ::= <year-component>
                | <year-component> <date-mark>
                | <date-span-component>
                | <date-span-component> <date-mark>
                | <year-component> <year-month-divider> <month-component>
                | <year-component> <year-month-divider> <month-day-component>

; MARK: ------------------------------------------------------------------------
; MARK: Cutter numbers

  <cutter-letter> ::= <letter>
  <cutter-number> ::= <cutter-letter> <digit16>
 <cutter-ordinal> ::= <digit16> <word>
    <cutter-mark> ::= <letter> <letter>
                    | <letter> <letter> <letter>

    <cutter-tail> ::= <space> <date>
                    | <space> <cutter-mark>
                    | <space> <cutter-mark> <space> <date>
 <cutter-divider> ::= '.'
                    | <space> '.'
                    | <space> <cutter-ordinal> <space> '.'
<cutter-list-end> ::= <cutter-letter>
                    | <cutter-number>
                    | <cutter-number> <cutter-tail>
                    | <cutter-number> <space> <cutter-ordinal>
                    | <cutter-number> <space> <cutter-ordinal> '.'
    <cutter-list> ::= <cutter-list-end>
                    | <cutter-number> <cutter-divider> <cutter-list>
                    | <cutter-number> <cutter-tail> <space> '.' <cutter-list>

; MARK: ------------------------------------------------------------------------
; MARK: Etcetera

            <etc> ::= ',' <space> "etc"
                    | ',' <space> "etc" '.'

; MARK: ------------------------------------------------------------------------
; MARK: Volume

  <volume-prefix> ::= <word> '.'
  <volume-suffix> ::= <digit16>
                    | <digit16> <etc>
         <volume> ::= <volume-prefix> <volume-suffix>
                    | <volume-prefix> <space> <volume-suffix>

; MARK: ------------------------------------------------------------------------
; MARK: Supplement

  <supplement-prefix> ::= <upper>
                        | <upper> <letter15>
  <supplement-suffix> ::= <space> <digit16>
                        | <space> <digit16> <etc>
         <supplement> ::= <supplement-prefix> <supplement-suffix>
                        | <supplement-prefix> '.' <supplement-suffix>

; MARK: ------------------------------------------------------------------------
; MARK: Specification

      <specification> ::= <date>
                        | <spec-ordinal>
                        | <supplement>
                        | <volume>
                        | <longword>
       <spec-ordinal> ::= <digit16> <spec-ordinal-suffix>
<spec-ordinal-suffix> ::= <word> '.'
                        | <word> '.' <spec-ordinal-suffix>

; MARK: ------------------------------------------------------------------------
; MARK: Classification subject

       <subclass> ::= <letter>
                    | <letter> <letter>
                    | <letter> <letter> <letter>
        <integer> ::= <digit03>
                    | <digit03> <digit03>
        <decimal> ::= '.' <digit16>
                    | ',' <digit16>

        <subject> ::= <subject-base>
                    | <subject-base> <space> <date>
                    | <subject-base> <space> <subject-ordinal>
   <subject-base> ::= <subclass>
                    | <subclass> <space> <integer>
                    | <subclass> <space> <integer> <decimal>
<subject-ordinal> ::= <digit16> <word>

; MARK: ------------------------------------------------------------------------
; MARK: Call number

    <call-number> ::= <subject>
                    | <subject> <call-cutters>
                    | <subject> <call-cutters> <call-spec-list>
   <call-cutters> ::= '.' <cutter-list>
                    | <space> '.' <cutter-list>
 <call-spec-list> ::= <space> <specification>
                    | <space> <specification> <call-spec-list>

